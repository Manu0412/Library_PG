@echo off

setlocal EnableExtensions

set "ROOT=%~dp0.."
set "PERSISTENCE=%ROOT%\Library.Persistence\Library.Persistence.csproj"
set "API=%ROOT%\Library.Api\Library.Api.csproj"
set "MIGRATIONS_DIR=Migrations"

cd /d "%ROOT%"

if "%~1" == "" goto usage
if "%~1" == "help" goto usage
if "%~1" == "h" goto usage
if "%~1" == "--help" goto usage

if "%~1" == "add" goto add
if "%~1" == "update" goto update
if "%~1" == "drop" goto drop

echo Comando desconocido: %~1
goto usage

:add
if "%~2" == "" (
	echo Error: falta el nombre de la migracion.
	goto usage
)
dotnet ef migrations add "%~2" --project "%PERSISTENCE%" --startup-project "%API%" --output-dir "%MIGRATIONS_DIR%"
goto end

:update
if "%~2" == "" (
	dotnet ef database update --project "%PERSISTENCE%" --startup-project "%API%"
) else (
	dotnet ef database update "%~2" --project "%PERSISTENCE%" --startup-project "%API%"
)
goto end

:drop
dotnet ef database drop --project "%PERSISTENCE%" --startup-project "%API%"
goto end

:usage
echo Uso: ef.cmd ^<comando^> [args]
echo.
echo Comandos:
echo    add ^<Nombre^>       Crea una migracion
echo    update [Nombre]    Aplica migraciones (todas o por nombre)
echo    drop               Elimina la base de datos
exit /b 1

:end
endlocal